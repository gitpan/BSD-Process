#! /usr/bin/perl -w

use strict;
use BSD::Process;
use Getopt::Std;

use vars '$VERSION';
$VERSION = '0.1';

getopts( 'nqrV', \my %opt );

print "$VERSION\n" and exit if $opt{V};

if ($opt{q}) {
    print join(' ', sort(BSD::Process::attr())), $/;
    exit;
}

my @attr;
for my $attr (@ARGV) {
    if (BSD::Process->can($attr)) {
        push @attr, $attr;
    }
    else {
        warn "ignoring unknown attribute $attr.\n";
    }
}

my @line;
my @width = (0) x (1 + @attr);

PROC:
for my $pid (sort {$a <=> $b} BSD::Process::list) {
    my $proc = BSD::Process->new(
        $pid,
        {
            resolve => exists($opt{r}) ? 1 : 0,
        }
    );
    my $id = "$proc->{comm}($proc->{pid})";
    my @out = map {$proc->{$_}} @attr;

    if ($opt{n}) {
        my $keep = 0;
        for my $attr (@out) {
            if ($attr != 0) {
                $keep = 1;
                last;
            }
        }
        next PROC unless $keep;
    }

    unshift @out, $id;
    for my $idx (0..$#out) {
        $width[$idx] = length($out[$idx])
            if $width[$idx] < length($out[$idx]);
    }
    push @line, [@out];
}

exit unless @line;

my @header = ('process', @attr);
for my $idx (0..$#header) {
    $width[$idx] = length($header[$idx])
        if $width[$idx] < length($header[$idx]);
}

my $fmt = join( ' ', map {'%' . -$_ . 's'} @width) . "\n";
printf $fmt, @header;
for my $ln (@line) {
    printf $fmt, @$ln;
}

=head1 NAME

showprocattr - Show attributes of current processes

=head1 SYNOPSIS

B<showprocattr> [B<-nrv>] attr [...]

=head1 DESCRIPTION

Display one or more attributes for all processes currently running
on the system.

=head1 OPTIONS

=over 4

=item B<-n>

Non-zero. Only display the process if all attributes are non-zero.

=item B<-q>

Query. Display all the reognised attribute names that may be examined.

=item B<-r>

Resolve. Display uids and gids as names, rather than their numerical
values.

=item B<-V>

Print the version of this program and exit.

=back

=head1 EXAMPLES

C<procshowattr -n kthread>

Show only the processes that are kernel threads.

=head1 SEE ALSO

L<BSD::Process>

=head1 AUTHOR

David Landgren, copyright (C) 2006. All rights reserved.

=head1 LICENSE

This program is free software; you may redistribute it and/or modify
it under the same terms as Perl itself.
